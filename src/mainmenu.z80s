MAINMENU:
                di

                xor a
                ld (NEXTDISPLAYSCREEN),a    ; frame interrupt shouldn't switch VMPR on us until we're doing animations

                ld a,SCREENPAGE0+96
                ld (DISPLAYSCREEN),a

 
                ld a,BORDERSCREENOFF
                out (BORDER),a
                LD   A,SCREENPAGESPECIAL
                out (HMPR),a

                or 96
                out (vmpr),a

                ld sp,HMEM_STACK_FRAME

                ld a,MUSIC.maaora.gzPAGE
                ld hl,MUSIC.maaora.gz
                call DECODEMUSIC
                call MUSIC_GOTO_CUSTOM

                ld a, LOGOPAGE
                ld hl, LOGOADDR

                call DECODEGZSCREEN    

                ld a,SCREENPAGE0
                call COPY_THIS_SCREEN_ONCE
                call DUPLICATESCREENDATA
                
                ld a,SCREENPAGE0
                out (hmpr),a
                or 96
                out (vmpr),a

				ld hl, MAINMENU_INTCALLBACK
				ld (FRAMEVECTOR),hl



                jp MAINLOOP
     
                
MAINMENU_INTCALLBACK:                
				ld a,(last_calculated_frame)
				inc a
				ld (last_calculated_frame),a    ; signal that we want the screen frame to update + cycle
				
				ld h, KEYBOARD_TABLE/256
				ld a, (player_space + PLAYER.FIRE_KEYaddr)
				ld l,a
				ld a, (player_space + PLAYER.FIRE_KEYmask)
				and (hl)
				or a
				jr z, mainmenu_startgame
		
		
		
		
				ld bc, MENU_STAR_EFFECT_ID
				call ADD_SPRITE_TO_CURRENT_LIST ; bc=routine, de=location (uses HL)
		
				
				ret



mainmenu_startgame:
				ld hl, Start_Game
				ld (EXIT_MAIN_LOOP_VECTOR), hl
                
                ret
                
               
Start_Game:
				di
				call SILENCE

                
                ld sp,HMEM_STACK_FRAME
				                
                xor a
                ld (NEXTDISPLAYSCREEN),a    ; frame interrupt shouldn't switch VMPR on us until we're doing animations

                ld a,SCREENPAGE0+96
setup_game_screens_loop:
                ld (DISPLAYSCREEN),a

				out (hmpr),a

				ld ix, drawpaneloutlines_rtn
				ld hl,32768 + 176*128
				exx


				ld b,16
				ld hl,panel_layout
drawpaneloutlines_loop:				
				ld e,(hl)
				inc l
				ld a,(hl)
				inc l
				
				exx
				ld d,a
				exx
				ld a,e
				exx
				ld e,a
				
				
assert (DRAWtiles.png_0_11PAGE == DRAWtiles.png_2_11PAGE)
				ld a,DRAWtiles.png_0_11PAGE | 32

				jp HMEM_SWITCH_LMEM_DRAW  ; SP is background remove stack, DE is routine in page A, HL is HMEM address to draw			
drawpaneloutlines_rtn:
				ld a,l
				sub 128-8
				ld l,a

				exx
				djnz drawpaneloutlines_loop


				ld bc,16
				ld hl, game_palette
				ld de,32768+24576
				ldir

				ld a,(DISPLAYSCREEN)
				add 2
				cp SCREENPAGE0+96+6
				jr c, setup_game_screens_loop

 
                ld a,BORDERSCREENOFF
                out (BORDER),a
                LD   A,SCREENPAGESPECIAL
                out (HMPR),a
                

                ld a,MUSIC.hector.gzPAGE
                ld hl,MUSIC.hector.gz
                call DECODEMUSIC
                call MUSIC_GOTO_CUSTOM


				ld hl, level1
				jp begin_screen

ds align 32
panel_layout:
				dw DRAWtiles.png_0_11,DRAWtiles.png_2_11
				dw DRAWtiles.png_0_11,DRAWtiles.png_1_11,DRAWtiles.png_2_11
				dw DRAWtiles.png_0_11,DRAWtiles.png_1_11,DRAWtiles.png_1_11,DRAWtiles.png_1_11,DRAWtiles.png_1_11,DRAWtiles.png_2_11
				dw DRAWtiles.png_0_11,DRAWtiles.png_1_11,DRAWtiles.png_1_11,DRAWtiles.png_1_11,DRAWtiles.png_2_11



;    DS ALIGN 16
;DRAW_STARS_ROUTINE_ID: EQU (2*$) + (@-PAGE)
;    DS (@-PAGE)/2


				   ;1234567890123456
MENU_TEXT_STATIC:
				dm "FIRE to start"
				db 13
				dm "ESC for menu"
				db 0   
MENU_TEXT_ROLLING:
				dm "MNEMOTECH"
				db 0
				dm "in association"
				db 13
				dm "with"
				db 0
				dm "SILVER SWORD"
				db 0
				dm "proudly presents"
				db 0
				dm "CYBERNAUT I."
				db 13,13
				dm "The Fighting"
				db 13
				dm "Machine"
				db 0
				MDAT "../_build/version"
				db 0
				dm "Programming by"
				db 13
				dm "Andrew Collier"
				db 0
				dm "Graphics by"
				db 13
				dm "Josef Prokes"
				db 0
				
				
